load("@bazel_skylib//rules:common_settings.bzl", "bool_flag")
load("@rules_rust//rust:defs.bzl", "rust_library", "rust_test", "rust_clippy", "rust_doc", "rustfmt_test")

# Define a build flag
bool_flag(
    name = "enable_chrono",
    build_setting_default = False,
)

# Create a config setting that references the flag
config_setting(
    name = "chrono_enabled",
    flag_values = {":enable_chrono": "True"},
)

rust_library(
    name = "swiftnav",
    srcs = [
        "src/lib.rs",
        "src/coords/ecef.rs",
        "src/coords/ned.rs",
        "src/reference_frame/mod.rs",
        "src/time/mjd.rs",
        "src/coords/ellipsoid.rs",
        "src/edc.rs",
        "src/reference_frame/params.rs",
        "src/time/mod.rs",
        "src/coords/llh.rs",
        "src/signal.rs",
        "src/time/utc.rs",
        "src/coords/mod.rs",
        "src/math.rs",
        "src/time/gnss.rs",
    ],
    deps = [
        "@crates//:strum",
        "@crates//:nalgebra",
        "@crates//:thiserror",
    ] + select({
          ":chrono_enabled": ["@crates//:chrono"],
          "//conditions:default": [],
    }),
    proc_macro_deps = [
        "@crates//:rustversion",
    ],
    edition = "2018",
    crate_features = select({
          ":chrono_enabled": ["chrono"],
          "//conditions:default": [],
    }),
    visibility = ["//visibility:public"],
)

rust_test(
    name = "swiftnav_unit_test",
    crate = ":swiftnav",
    deps = [
        "@crates//:float_eq",
        "@crates//:proptest",
    ],
    crate_features = select({
          ":chrono_enabled": ["chrono"],
          "//conditions:default": [],
    }),
)

rustfmt_test(
    name = "swiftnav_fmt_test",
    targets = [
        ":swiftnav",
        ":swiftnav_unit_test",
    ]
)

rust_clippy(
    name = "swiftnav_clippy",
    testonly = True,
    deps = [
        ":swiftnav",
        ":swiftnav_unit_test",
    ],
)

rust_doc(
    name = "swiftnav_doc",
    crate = ":swiftnav",
    visibility = ["//visibility:public"],
)